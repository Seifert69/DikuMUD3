Generate MCP entry for DIL keyword: cast_spell

=== Yacc rule ===
| DILSE_CAST '(' dilexp ',' dilexp ',' dilexp ',' dilexp ',' dilexp ')'
{
    INITEXP($$);
    checkbool("argument 1 of cast", $3.boolean);

    if ($3.typ != DilVarType_e::DILV_INT)
    {
        dilfatal("Arg 1 of 'cast_spell' not a number");
    }
    else if ($5.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 2 of 'cast_spell' not a unitptr");
    }
    else if ($7.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 3 of 'cast_spell' not a unitptr");
    }
    else if ($9.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 4 of 'cast_spell' not a unitptr");
    }
    else if ($11.typ != DilVarType_e::DILV_SP)
    {
        dilfatal("Arg 5 of 'cast_spell' not a string");
    }
    else
    {
        /* Type is ok */
        /* Make nodes dynamic */
        $$.dsl = DSL_DYN;
        $$.typ = DilVarType_e::DILV_INT;

        make_code(&($3));
        make_code(&($5));
        make_code(&($7));
        make_code(&($9));
        make_code(&($11));

        add_code(&($$), &($3));
        add_code(&($$), &($5));
        add_code(&($$), &($7));
        add_code(&($$), &($9));
        add_code(&($$), &($11));
        add_ubit8(&($$), DILE_CAST2);
    }

    FREEEXP($3);
    FREEEXP($5);
    FREEEXP($7);
    FREEEXP($9);
    FREEEXP($11);
}

------

| DILSE_CAST '(' coreexp ',' coreexp ',' coreexp ',' coreexp ')' ihold
{
    checkbool("argument 1 of cast", $3.boolean);
    if ($3.typ != DilVarType_e::DILV_INT)
    {
        dilfatal("Arg 1 of 'cast_spell' not a number");
    }
    else if ($5.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 2 of 'cast_spell' not a unitptr");
    }
    else if ($7.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 3 of 'cast_spell' not a unitptr");
    }
    else if ($9.typ != DilVarType_e::DILV_UP)
    {
        dilfatal("Arg 4 of 'cast_spell' not a unitptr");
    }
    else
    {
        $$.fst = $3.fst;
        $$.lst = $11 + 1;
        wtmp = &tmpl.core[$11];
        bwrite_ubit8(&wtmp, DILI_CAST);
    }
}

=== C implementation ===
void dilfi_cast(dilprg *p)
{
    dilval *v4 = p->stack.pop();
    dilval *v3 = p->stack.pop();
    dilval *v2 = p->stack.pop();
    dilval *v1 = p->stack.pop();
    unit_data *caster = nullptr;
    unit_data *medium = nullptr;
    unit_data *target = nullptr;

    p->waitcmd--;

    if (dil_type_check("cast",
                       p,
                       4,
                       v1,
                       TYPEFAIL_NULL,
                       1,
                       DILV_INT,
                       v2,
                       TYPEFAIL_NULL,
                       1,
                       DILV_UP,
                       v3,
                       TYPEFAIL_NULL,
                       1,
                       DILV_UP,
                       v4,
                       TYPEFAIL_NULL,
                       1,
                       DILV_UP))
    {
        caster = (unit_data *)v2->val.ptr;
        medium = (unit_data *)v3->val.ptr;
        target = (unit_data *)v4->val.ptr;

        if (is_in(v1->val.num, SPL_GROUP_MAX, SPL_TREE_MAX - 1) && caster && caster->isChar() && medium &&
            (g_spell_info[v1->val.num].spell_pointer || g_spell_info[v1->val.num].tmpl))
        {
            /* cast the spell */
            spell_perform(v1->val.num, MEDIA_SPELL, caster, medium, target, "");
            dil_test_secure(p);
        }
    }

    delete v1;
    delete v2;
    delete v3;
    delete v4;
}

=== DIL example ===
dilbegin item_charge(base_charge:integer);
external
  string accept_dil@function (pc:unitptr,s:string);

var
   tcost   : integer;
   state   : integer;
   magmod  : integer;
   addmag  : integer;
   die     : integer;
   result  : integer;
   percent : integer;
   thing   : unitptr;
   pc      : unitptr;
   answer  : string;
   mn      : string;

code
{

:init:
   heartbeat := PULSE_SEC*5;
   self.spells[SPL_RECONSTRUCT] := 200;
   self.abilities[ABIL_BRA] := 150;
   self.abilities[ABIL_MAG] := 150;   
   position_update(self);
:start:
   wait (SFB_DONE,( (command("give")) and (self==target) ));

pc:=activator;
thing:=medium;
secure(pc,lost_pc);
secure(thing,lost_pc);

if( not visible( self, pc ) )
   {
      exec("say I only do business with people I can see.", self );
      exec("drop " + (thing.name), self);
      goto lost_pc;
   }

dilcopy ("busy@function(Please wait I am busy with other work right now.",
self)
;

if (thing.type!=UNIT_ST_OBJ)
   {
      exec ("say How am I supposed to recharge that then "+pc.name+"?",self);
      exec ("bonk " + pc.name,self);
      goto give_back;
   }

if ((thing.objecttype!=ITEM_WAND) and (thing.objecttype!=ITEM_STAFF))
   {
      exec ("say How am I supposed to recharge that then "+pc.name+"?",self);
      exec ("bonk " + pc.name,self);
      goto give_back;
   }

/* If people don't wish their wands/staffs rechargable by shops, they declare
   an extra, $norecharge, on the item. */

if ("$norecharge" in thing.extra)
   {
      exec ("say I can't recharge this " + thing.title +
            "," + pc.name +". It is beyond my ability.", self);
      goto give_back;
   }

if ( thing.value[1] == thing.value[4] )
   {
      exec("say It is fully charged, what do you want " +
           "to go charging it for?!?", self);
      exec("bonk " + pc.name, self);
      goto give_back;
   }

state:=thing.value[4]-thing.value[1];
goto how_much;


:broken_thing:
   exec("say Oh my, it's broken. I can't fix that. I suggest " +
        "you try to obtain another.", self);
   goto give_back;

:not_possible:
   exec("say Oh dear, I'm afraid that it's past the point " +
           "of no return. Repairing it would only make it " +
           "worse " + pc.name + ".", self);
   goto give_back;

:how_much:
   tcost := (base_charge*160); /* Base cost in 1 gold 2 sp. Modified by
                                   how badly damaged the item is and its
                                   Magical Modifier */
   tcost := tcost * state;     /* Adjust for how bad the damage is */
   magmod := thing.value[0]; /* Take spell power into account    */

   if (magmod > 0)
   {
      addmag := magmod; /* 1 gp per mag point */
      tcost := tcost * addmag;  /* Final cost         */
   }

   mn := moneystring(tcost, 0);
   exec ("say It will cost you " + mn + " for me to " +
         "recharge that for you.", self);
   answer := accept_dil@function(pc, "Are you sure you want me to recharge it?");
   pause;

   if (answer=="no")
   {
      exec("say Very well. Come back to me when you want it recharged.",self);
      goto give_back;
   }

   if (transfermoney(pc, self, tcost))
   {
      exec(", touches " + thing.name + " softly and begins to chant...", self);
:repair_it:
      if(thing.objecttype==ITEM_WAND)
        result := cast_spell(SPL_CHARGE_WAND, self, self, thing, "");
      else
        result := cast_spell(SPL_CHARGE_STAFF, self, self, thing, "");
      if (thing.value[1] < thing.value[4]) goto repair_it;
      heartbeat:=PULSE_SEC*1;
      pause;
      exec("say There, hope you like it, " + pc.name + "!", self);
      heartbeat:=PULSE_SEC*5;
      goto give_back;
   }
   else goto no_money;

:no_money:
   exec ("say You can't afford my services right now. Please come back " +
         "when you have enough money.",self);
   goto give_back;

:give_back:
   act( self.name + " returns your " + thing.name + " to you.",
       A_ALWAYS,pc,null,null,TO_CHAR);
   act(self.name + " returns $1n's " + thing.name + " to $1m.",
       A_SOMEONE,pc,null,null,TO_REST);
   link(thing,pc);
   goto lost_pc;

:lost_pc:
   die := dildestroy ("busy@function",self);
   unsecure(pc);
   unsecure(thing);
   goto start;
} dilend
